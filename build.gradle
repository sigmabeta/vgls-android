buildscript {
    ext {
        kotlin_version = '1.3.50'
        okhttp_version = '4.2.0'
        retrofit_version = '2.6.1'
        room_version = "2.2.0-rc01"
        rxjava_version = "2.2.12"
        rxandroid_version = "2.1.1"
        rxbinding_version = "3.0.0-alpha2"
        mvrx_version = "1.1.0"
        dagger_version = "2.24"
        assistedinject_version = "0.5.0"
        stetho_version = "1.5.1"
        viewpager_version = "1.0.0-beta04"
        core_ktx_version = "1.1.0"
        appcompat_version = "1.1.0"
        material_version = "1.1.0-alpha10"
        recycler_version = "1.0.0"
        timber_version = "4.7.1"
        junit_version = "4.12"
        espresso_runner_version = "1.0.2"
        espresso_core_version = "3.0.2"
        picasso_version = "2.71828"
        constraint_version = "2.0.0-beta2"
        threeten_version = "1.2.1"
        photoview_version = "2.0.0"
        firebase_version = "17.2.0"
        crashlytics_version = "2.10.1"
        firebase_perf_version = "19.0.0"
        simplestore_version = "0.0.7"
    }

    repositories {
        google()
        jcenter()

        // Firebase (Crashlytics)
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        // Android, lol
        classpath 'com.android.tools.build:gradle:3.5.1'

        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // Firebase
        classpath 'com.google.gms:google-services:4.3.2'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
        classpath 'io.fabric.tools:gradle:1.31.1'
    }
}

plugins {
    // Spotless / KTLint
    id "com.diffplug.gradle.spotless" version "3.24.3"

    // Detekt
    id "io.gitlab.arturbosch.detekt" version "1.0.1"
}

allprojects {
    // Detekt
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        config = files("$rootDir/default-detekt-config.yml")
        filters = ".*build.*,.*/resources/.*,.*/tmp/.*"
    }

    repositories {
        google()
        jcenter()

        // For PhotoView
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'com.diffplug.gradle.spotless'

    android {
        compileSdkVersion 29
        defaultConfig {
            minSdkVersion 21
        }
    }

    spotless {
        kotlin {
            target "**/*.kt"
            ktlint()
        }
    }

    // Treat all Kotlin warnings as errors
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
}
